// Start with a list of VMs, format the resourceId in lowercase for joining to the maintenanceresources later
// Do the join with mainteinanceresources looking specifically for configurationassignments
// Filter to only return resources that have no maintenance configuration assigned
Resources
| extend resourceId = tolower(id)
| project name, location, type, id, resourceId, properties
| where type =~ 'Microsoft.Compute/virtualMachines'
| join kind=leftouter (
maintenanceresources
| where type =~ "microsoft.maintenance/configurationassignments"
| project planName = name, type, maintenanceProps = properties
| extend resourceId = tostring(maintenanceProps.resourceId)
) on resourceId
| where isnull(maintenanceProps)
| project recommendationId = "vm-22",name, location, type, id, properties
| order by id asc
