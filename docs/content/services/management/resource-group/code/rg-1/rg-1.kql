// Azure Resource Graph Query
// Provides a list of Azure Resource Groups that have resources deployed in a region different than the Resource Group region
resources
| project id, name, tags, resourceGroup, location
| where location != "global"                                                                                                          // exclude global resources
| where resourceGroup != "networkwatcherrg"                                                                                           // exclude networkwatcherrg
| where split(id, "/", 3)[0] =~ "resourceGroups"                                                                                      // resource is in a resource group
| extend resourceGroupId = strcat_array(array_slice(split(id, "/"),0,4), "/")                                                         // create resource group resource id
| join (resourcecontainers | project containerid=id, containerlocation=location ) on $left.resourceGroupId == $right.['containerid']  // join to resourcecontainers table
| where  location != containerlocation
| project recommendationId="rg-1", name, id, tags
| order by id asc
