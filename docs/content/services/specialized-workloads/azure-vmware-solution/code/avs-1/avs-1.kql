// Azure Resource Graph Query
// Provides a list of Private Clouds without monitoring thresholds for vSAN datastore consumption.
resources
| where ['type'] == "microsoft.avs/privateclouds"
| extend scopeId = tostring(id)
| project ['scopeId']
| join kind=leftouter (
resources
| where type == "microsoft.insights/metricalerts"
| extend alertProperties = todynamic(properties)
| mv-expand alertProperties.scopes
| mv-expand alertProperties.criteria.allOf
| extend scopeId = tostring(alertProperties_scopes)
| extend metric = alertProperties_criteria_allOf.metricName
| extend threshold = alertProperties_criteria_allOf.threshold
| project scopeId, tostring(metric), toint(['threshold'])
| where metric == "DiskUsedPercentage"
| where threshold == 70
) on scopeId
| where isnull(['threshold'])
