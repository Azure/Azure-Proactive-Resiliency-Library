// Azure Resource Graph Query
// Provides a list of Private Clouds without monitoring memory utilization.
resources
| where ['type'] == "microsoft.avs/privateclouds"
| extend scopeId = tostring(id)
| project ['scopeId']
| join kind=leftouter (
resources
| where type == "microsoft.insights/metricalerts"
| extend alertProperties = todynamic(properties)
| mv-expand alertProperties.scopes
| mv-expand alertProperties.criteria.allOf
| extend scopeId = tostring(alertProperties_scopes)
| extend metric = alertProperties_criteria_allOf.metricName
| project scopeId, metric
| where metric == "UsageAverage"
) on scopeId
| where isnull(metric)
