// Azure Resource Graph Query
// Find all Availability Sets with VMs with different name prefix. This query is to help identify different DBW roles sharing the same AvailabilitySet.
// Customize the query to meet your naming standards, replace the "5" with the number of characters you want to compare (name, 0, 5)
Resources
| where type =~ 'Microsoft.Compute/virtualMachines'
| where isnotnull(properties.availabilitySet)
| extend vmPrefix = substring(name, 0, 5)
| summarize VMs = make_set(vmPrefix) by availabilitySet = tostring(properties.availabilitySet.id)
| where array_length(VMs) > 1
| extend availabilitySetName = tostring(split(availabilitySet, '/')[8])
| project recommendationId = "dbw-3", name=availabilitySetName, id="", param1=strcat("availabilitySet: ",availabilitySet), param2 = strcat("VMs :", VMs)
