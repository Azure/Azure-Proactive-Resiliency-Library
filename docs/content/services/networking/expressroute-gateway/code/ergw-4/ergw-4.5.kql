// Azure Resource Graph Query
// Provides a list of ExpressRoute Gateways that are not currently monitoring the Average Packets per Second to ensure it stays below a critical threshold per AMBA standard.
//To remediate this finding, create an alert with the following configuration on each failing gateway: Operator: GreaterThan, metric: ExpressRouteGatewayPacketsPerSecond, timeAggregation: Average, Threshold: <sku dependent maximum>, Severity: 0
resources
| where type == "microsoft.network/virtualnetworkgateways"
| where properties.gatewayType == "ExpressRoute"
| extend gatewayId = tostring(id)
| extend skuName = properties.sku.name
| extend packetsPerSecondSku = case(skuName == 'Standard', 100000,
                                    skuName == 'ErGw1AZ', 100000,
                                    skuName == 'HighPerformance', 200000,
                                    skuName == 'ErGw2AZ', 200000,
                                    skuName == 'UltraPerformance', 1000000,
                                    skuName == 'ErGw3AZ', 1000000,
                                    skuName == 'ErGwScale', 100000,
                                    100000)
| project gatewayId, name, id, tags, skuName, packetsPerSecondSku
| join kind=leftouter(
resources
| where type == 'microsoft.insights/metricalerts'
| extend alertProperties = todynamic(properties)
| mv-expand alertProperties.scopes
| mv-expand alertProperties.criteria.allOf
| where alertProperties.enabled == true
| extend gatewayId = tostring(alertProperties_scopes)
| extend criterionType = alertProperties_criteria_allOf.criterionType
| extend metric = alertProperties_criteria_allOf.metricName
| extend metricNamespace = alertProperties_criteria_allOf.metricNamespace
| extend operator = alertProperties_criteria_allOf.operator
| extend threshold = alertProperties_criteria_allOf.threshold
| extend timeAggregation = alertProperties_criteria_allOf.timeAggregation
| extend severity = alertProperties.severity
| where alertProperties.evaluationFrequency == 'PT1M'
| where alertProperties.windowSize == 'PT5M'
| where metricNamespace == 'Microsoft.Network/virtualNetworkGateways'
| where timeAggregation == 'Average'
| where metric == 'ExpressRouteGatewayPacketsPerSecond'
| where operator == 'GreaterThan'
| where severity == 0
) on gatewayId
| where threshold != packetsPerSecondSku
| where isnull(threshold)
| project recommendationId = 'ergw-4', name, id, tags, param1 = 'monitorExpressRouteGatewayHealth', param2 = 'ExpressRouteGatewayHealthCategory:Performance', param3 = 'ExpressRouteGatewayPacketsPerSecond greater than sku maximum'
