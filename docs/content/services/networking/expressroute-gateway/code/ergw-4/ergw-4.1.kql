// Azure Resource Graph Query
// Provides a list of ExpressRoute Gateways that are not currently monitoring the Maximum number of routes advertised by to peer limit on the gateway.
//To remediate this finding, create an alert with the following configuration on each failing gateway: Operator: GreaterThanOrEqual, metric: ExpressRouteGatewayCountOfRoutesAdvertisedToPeer, timeAggregation: Maximum, Threshold 500
resources
| where type == "microsoft.network/virtualnetworkgateways"
| where properties.gatewayType == "ExpressRoute"
| extend gatewayId = tostring(id)
| extend skuName = properties.sku.name
| project gatewayId, name, id, tags, skuName
| join kind=leftouter(
resources
| where type == 'microsoft.insights/metricalerts'
| extend alertProperties = todynamic(properties)
| mv-expand alertProperties.scopes
| mv-expand alertProperties.criteria.allOf
| where alertProperties.enabled == true
| extend gatewayId = tostring(alertProperties_scopes)
| extend criterionType = alertProperties_criteria_allOf.criterionType
| extend metric = alertProperties_criteria_allOf.metricName
| extend metricNamespace = alertProperties_criteria_allOf.metricNamespace
| extend operator = alertProperties_criteria_allOf.operator
| extend threshold = alertProperties_criteria_allOf.threshold
| extend timeAggregation = alertProperties_criteria_allOf.timeAggregation
| where metricNamespace == 'Microsoft.Network/virtualNetworkGateways'
| where timeAggregation == 'Maximum'
| where metric == 'ExpressRouteGatewayCountOfRoutesAdvertisedToPeer'
| where operator == 'GreaterThanOrEqual'
| where threshold == 500
) on gatewayId
| where isnull(threshold)
| project recommendationId = 'ergw-4', name, id, tags, param1 = 'monitorExpressRouteGatewayHealth', param2 = 'ExpressRouteGatewayHealthCategory:Availability', param3 = 'ExpressRouteGatewayCountOfRoutesAdvertisedToPeer at or exceeding sku limit'
