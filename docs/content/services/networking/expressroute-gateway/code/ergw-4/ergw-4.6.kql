// Azure Resource Graph Query
// Provides a list of ExpressRoute Gateways that are not currently monitoring the Maximum number of flow limit on the gateway based on the gateway sku.
//To remediate this finding, create an alert with the following configuration on each failing gateway: metric: ExpressRouteGatewayActiveFlows, timeAggregation: Maximum, Operator: GreaterThanOrEqual, Threshold 100000 for Standard, ERGW1AZ, 200000 for High Performance and ERGW2aZ, and 1000000 for Ultra and ERGW3AZ.
resources
| where type == "microsoft.network/virtualnetworkgateways"
| where properties.gatewayType == "ExpressRoute"
| extend gatewayId = tostring(id)
| extend skuName = properties.sku.name
| extend flowsPerSku = case(skuName == 'Standard', 100000,
                                    skuName == 'ErGw1AZ', 100000,
                                    skuName == 'HighPerformance', 200000,
                                    skuName == 'ErGw2AZ', 200000,
                                    skuName == 'UltraPerformance', 1000000,
                                    skuName == 'ErGw3AZ', 1000000,
                                    skuName == 'ErGwScale', 100000,
                                    100000)
| project gatewayId, name, id, tags, skuName, flowsPerSku
| join kind=leftouter(
resources
| where type == 'microsoft.insights/metricalerts'
| extend alertProperties = todynamic(properties)
| mv-expand alertProperties.scopes
| mv-expand alertProperties.criteria.allOf
| where alertProperties.enabled == true
| extend gatewayId = tostring(alertProperties_scopes)
| extend criterionType = alertProperties_criteria_allOf.criterionType
| extend metric = alertProperties_criteria_allOf.metricName
| extend metricNamespace = alertProperties_criteria_allOf.metricNamespace
| extend operator = alertProperties_criteria_allOf.operator
| extend threshold = alertProperties_criteria_allOf.threshold
| extend timeAggregation = alertProperties_criteria_allOf.timeAggregation
| where metricNamespace == 'Microsoft.Network/virtualNetworkGateways'
| where timeAggregation == 'Maximum'
| where metric == 'ExpressRouteGatewayActiveFlows'
| where operator == 'GreaterThanOrEqual'
) on gatewayId
| where threshold != flowsPerSku
| where isnull(threshold)
| project recommendationId = 'ergw-4', name, id, tags, param1 = 'monitorExpressRouteGatewayHealth', param2 = 'ExpressRouteGatewayHealthCategory:Scalability', param3 = 'ExpressRouteGatewayActiveFlows at or exceeding sku limit'
