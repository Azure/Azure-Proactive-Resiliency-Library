// Azure Resource Graph Query
// Find all Virtual Network Gateways without Maintenance Configurations

resources
| where type =~ "Microsoft.Network/virtualNetworkGateways"
| extend resourceId = tolower(id)
| join kind=leftouter  (
    maintenanceresources
    | where type =~ "Microsoft.Maintenance/configurationAssignments"
    | project JsonData = parse_json(properties)
    | extend maintenanceConfigurationId = tolower(tostring(JsonData.maintenanceConfigurationId))
    | join kind=inner (
        resources
        | where type =~ "Microsoft.Maintenance/maintenanceConfigurations"
        | project maintenanceConfigurationId=tolower(id)
    ) on maintenanceConfigurationId
    | project maintenanceConfigurationId, resourceId=tolower(tostring(JsonData.resourceId))
) on resourceId
| where isempty(maintenanceConfigurationId)
| project recommendationId = "ergw-7", name, id, tags, param1= strcat("sku-tier: " , properties.sku.tier), param2=location
