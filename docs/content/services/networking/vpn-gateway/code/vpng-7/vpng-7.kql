resources
| where type =~ "Microsoft.Network/virtualNetworkGateways"
| where properties.gatewayType == "Vpn"
| where properties.sku.tier contains 'AZ'
| mv-expand ipconfig = properties.ipConfigurations
| extend pipId = tostring(ipconfig.properties.publicIPAddress.id)
| join kind=inner (
    resources
    | where type == "microsoft.network/publicipaddresses"
    | where isnull(zones) or array_length(zones) < 3   )
    on $left.pipId == $right.id
| project recommendationId = "vpng-7", VpnGateway= name, VpnGatewayId = id, VpnGatewayTags = tags, PublicIpAddress = name1, PublicIpAddressId = id1, PublicIpAddressTags = tags1
