// Azure Resource Graph Query
// List all Azure Firewalls resources in-scope, along with any metrics associated to Azure Monitor alert rules
resources
| where type == "microsoft.insights/metricalerts"
| mv-expand properties.scopes
| mv-expand properties.criteria.allOf
| project firewallId = properties_scopes, monitoredMetric = properties_criteria_allOf.metricName, name
| summarize monitoredMetrics = make_list(monitoredMetric) by tostring(firewallId)
| join kind = fullouter (
    resources
    | where type == "microsoft.network/azurefirewalls"
    | project rightFirewallId = id, name, id
    ) on $left.firewallId == $right.rightFirewallId
| where monitoredMetrics !contains("FirewallHealth") and monitoredMetrics !contains("Throughput") and monitoredMetrics !contains("SNATPortUtilization")
| project recommendationId = "afw-3", name, id, param1 = strcat("MetricsAlerts:", monitoredMetrics)
