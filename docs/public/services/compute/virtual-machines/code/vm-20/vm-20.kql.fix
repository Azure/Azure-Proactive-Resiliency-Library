// Azure Resource Graph Query
// Find all VMs that do not have the VM Insights extension installed
resources
| where type == 'microsoft.compute/virtualmachines'
| extend
    JoinID = toupper(id),
    vmName = name,
    OSType = tostring(properties.storageProfile.osDisk.osType)
| join kind=leftouter(
    resources
    | where type == 'microsoft.compute/virtualmachines/extensions'
    | extend
        VMId = toupper(substring(id, 0, indexof(id, '/extensions'))),
        ExtensionName = name
) on $left.JoinID == $right.VMId
| summarize param2 = strcat ("Extensions: ", make_list(ExtensionName)) by recommendationId="vm-20", name=vmName, id, tags=strcat(tags), param1=OSType
| where param2 !contains "MicrosoftMonitoringAgent" and param2 !contains "OMSAgentforLinux" and param2 !contains "AzureMonitorWindowsAgent" and param2 !contains "AzureMonitorLinuxAgent"
| order by tolower(name) asc
